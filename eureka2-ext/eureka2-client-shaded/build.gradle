/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'osgi'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    provided project(':eureka2-core')
    provided project(':eureka2-client')
    provided project(':eureka2-ext:eureka2-eureka1-support')

    // transitive dependencies from the shaded packages that needs to be explicitly specified
    // from eureka2-core
    compile "org.slf4j:slf4j-api:${slf4j_version}"
    compile "io.reactivex:rxjava:${rxjava_version}"

    // from eureka2-core -> rxNetty
    compile "io.netty:netty-codec-http:${shaded_netty_version}"
    compile "io.netty:netty-transport-native-epoll:${shaded_netty_version}"

    // from eureka2-core -> avro
    // none as all are shaded

    // from eureka2-client
    compile "javax.inject:javax.inject:1"

    // from eureka2-eureka1-support
    compile "com.netflix.eureka:eureka-client:${eureka_v1_version}"
}

shadowJar {
    dependencies {
        include(project(':eureka2-core'))
        include(project(':eureka2-client'))
        include(project(':eureka2-ext:eureka2-eureka1-support'))

        // avro and dependencies
        include(dependency('org.apache.avro:avro'))
        include(dependency('com.thoughtworks.paranamer:paranamer'))
        include(dependency('org.xerial.snappy:snappy-java'))
        include(dependency('org.apache.commons:commons-compress'))
        include(dependency('org.codehaus.jackson:jackson-core-asl'))
        include(dependency('org.codehaus.jackson:jackson-mapper-asl'))
        include(dependency('org.tukaani:xz'))

        // rxNetty
        include(dependency('io.reactivex:rxnetty'))
    }

    classifier null

    relocate 'org.apache.avro', 'com.eureka2.shading.apache.avro'
    relocate 'com.thoughtworks.paranamer', 'com.eureka2.shading.thoughtworks.paranamer'
    relocate 'org.xerial.snappy', 'com.eureka2.shading.xerial.snappy'
    relocate 'org.apache.commons.compress', 'com.eureka2.shading.apache.commons.compress'
    relocate 'org.codehaus.jackson', 'com.eureka2.shading.codehaus.jackson'
    relocate 'org.tukaani.xz', 'com.eureka2.shading.tukaani.xz'

    relocate 'io.reactivex.netty', 'com.eureka2.shading.reactivex.netty'
}

jar.deleteAllActions()
jar.dependsOn shadowJar

test {
    useJUnit()
}
