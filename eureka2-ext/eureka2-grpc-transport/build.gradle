/*
 * Copyright 2015 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'osgi'
apply plugin: 'com.google.protobuf'

configurations {
    grpcCompile
}

dependencies {
    compile project(':eureka2-api')

    compile "com.google.protobuf:protobuf-java:3.0.0-beta-1"
    compile "io.grpc:grpc-protobuf:0.9.0"
    compile "io.grpc:grpc-stub:0.9.0"
    compile "io.grpc:grpc-netty:0.9.0"

    testCompile project(':eureka2-test-utils')
    testCompile project(':eureka2-testkit')
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-1'
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:0.9.0'
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {
                }
            }
        }
    }
}

sourceSets {
    grpc {
        compileClasspath += configurations.grpcCompile
    }
}

jar {
    sourceSets.all { sourceSet ->
        from sourceSet.output
        dependsOn sourceSet.getCompileTaskName('java')
    }
    manifest {
        name = 'eureka2-grpc-transport'
        instruction 'Bundle-Vendor', 'Netflix'
        instruction 'Bundle-DocURL', 'https://github.com/Netflix/eureka'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
    }
}

// Let intellij projects refer to generated code
idea {
    module {
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/java");
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc");
    }
}
