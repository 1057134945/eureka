/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'osgi'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile project(':eureka2-core')
    compile "javax.inject:javax.inject:1"

    // As IDEs do not play well with shadowed dependencies, we should depend on
    // the original jar when doing development
    if (project.hasProperty('eureka2.devmode') || System.properties['eureka2.devmode']) {
        println "In development mode - adding compile level dependency to Ocelli"
        compile "com.netflix.ocelli:ocelli-core:${ocelli_version}"
    } else {
        println "Shadowing dependency to Ocelli"
        provided "com.netflix.ocelli:ocelli-core:${ocelli_version}"
    }

    testCompile project(':eureka2-test-utils')
}

jar {
    manifest {
        name = 'eureka2-client'
        instruction 'Bundle-Vendor', 'Netflix'
        instruction 'Bundle-DocURL', 'https://github.com/Netflix/eureka'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
    }
}

shadowJar {
    dependencies {
        include(dependency('com.netflix.ocelli:ocelli-core'))
    }
    classifier null
    relocate 'netflix.ocelli', 'com.eureka2.shading.netflix.ocelli'
}
jar.deleteAllActions()
jar.dependsOn shadowJar
