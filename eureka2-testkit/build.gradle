/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

dependencies {
    compile project(':eureka2-client')
    compile project(':eureka2-read-server')
    compile project(':eureka2-write-server')
    compile project(':eureka2-dashboard')
    compile "com.netflix.karyon:karyon2-admin-healthcheck-plugin:${karyon_version}"
    compile project(':eureka2-ext:eureka2-karyon-admin')
    compile project(':eureka2-ext:eureka2-eureka1-rest-api')
    compile "jline:jline:${jline_version}"
    compile "junit:junit:${junit_version}"
    compile "org.hamcrest:hamcrest-library:${hamcrest_version}"
    testCompile project(':eureka2-test-utils')
}

task runCluster (dependsOn: [classes], type: JavaExec) {
    group = "Examples"
    description = "Run Eureka cluster (default -Pwrite=1 -Pread=1)"

    main = "com.netflix.eureka2.testkit.embedded.EmbeddedRunner"
    classpath = sourceSets.main.runtimeClasspath

    args = [
            project.hasProperty("write") ? write : 1,
            project.hasProperty("read") ? read : 1,
            false, // bridge
            false,  // dashboard
            false,  // deployment view
            false, // extensions
            true,  // admin UI
            true   // ephemeral ports
    ]
}

// Why not gradle task? Because of issue https://issues.gradle.org/browse/GRADLE-1147
build << {
    buildDir.mkdirs()
    File script = new File(buildDir, 'cli.sh')

    script.withPrintWriter {
        it.println '#!/bin/sh'
        it.println "java -cp ${getRuntimeClasspath()} com.netflix.eureka2.testkit.cli.EurekaCLI \"\$@\""
    }

    // make it executable
    ant.chmod(file: script.absolutePath, perm: 'u+x')
}

String getRuntimeClasspath() {
    sourceSets.main.runtimeClasspath.collect { it.absolutePath }.join(':')
}
